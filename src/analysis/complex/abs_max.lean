/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.complex.cauchy_integral
import analysis.convex.integral

/-!
# Maximum modulus principle

In this file we prove several versions of the maximum modulus principle and Liouville's Theorem.

There are several statements that can be called "the maximum modulus principle" for maps between
normed complex spaces.

In the most general case, see `complex.norm_eventually_eq_of_is_local_max`, we can only say that for
a differentiable function `f : E â†’ F`, if the norm has a local maximum at `z`, then *the norm* is
constant in a neighborhood of `z`.

If the domain is a nontrivial finite dimensional space, then this implies the following version of
the maximum modulus principle, see `complex.exists_mem_frontier_is_max_on_norm`. If `f : E â†’ F` is
complex differentiable on a nonempty compact set `K`, then there exists a point `z âˆˆ frontier K`
such that `Î» z, âˆ¥f zâˆ¥` takes it maximum value on `K` at `z`.

Finally, if the codomain is a strictly convex space, then the function cannot have a local maximum
of the norm unless the function (not only its norm) is a constant. This version is not formalized
yet.

## TODO

All theorems in this file assume that the codomain is a complete space with second countable
topology. Both assumption can and should be removed, either during the planned refactor of the
Bochner integral, or by applying current version to the completion of the span of the range of `f`.
-/

open topological_space metric set filter asymptotics function measure_theory
open_locale topological_space filter nnreal real

universes u v w
variables {E : Type u} [normed_group E] [normed_space â„‚ E]
  {F : Type v} [normed_group F] [normed_space â„‚ F] [measurable_space F] [borel_space F]
    [second_countable_topology F] [complete_space F]

namespace complex

/-- Auxiliary lemma, use `complex.norm_eq_norm_of_is_max_on_of_closed_ball_subset` instead. -/
lemma norm_eq_norm_of_is_max_on_of_closed_ball_subset_dim1
  {f : â„‚ â†’ F} {s : set â„‚} {z w : â„‚} (hd : differentiable_on â„‚ f s) (hz : is_max_on (norm âˆ˜ f) s z)
  (hsub : closed_ball z (dist w z) âŠ† s) :
  âˆ¥f wâˆ¥ = âˆ¥f zâˆ¥ :=
begin
  set r := dist w z,
  have hw_mem : w âˆˆ closed_ball z r, from mem_closed_ball.2 le_rfl,
  refine (is_max_on_iff.1 hz _ (hsub hw_mem)).antisymm (not_lt.1 _),
  rintro hw_lt : âˆ¥f wâˆ¥ < âˆ¥f zâˆ¥,
  have hr : 0 < r, from dist_pos.2 (Î» h, hw_lt.ne $ h â–¸ rfl),
  have hsub' : sphere z r âŠ† s, from sphere_subset_closed_ball.trans hsub,
  have hne : âˆ€ Î¶ âˆˆ sphere z r, Î¶ â‰  z,
    from Î» Î¶ hÎ¶, ne_of_mem_of_not_mem hÎ¶ (ne_of_lt $ (dist_self z).symm â–¸ hr),
  have hcont : continuous_on (Î» Î¶, (Î¶ - z)â»Â¹ â€¢ f Î¶) (sphere z r),
    from ((continuous_on_id.sub continuous_on_const).invâ‚€ $
      Î» Î¶ hÎ¶, sub_ne_zero.2 (hne Î¶ hÎ¶)).smul (hd.continuous_on.mono hsub'),
  have hle : âˆ€ Î¶ âˆˆ sphere z r, âˆ¥(Î¶ - z)â»Â¹ â€¢ f Î¶âˆ¥ â‰¤ âˆ¥f zâˆ¥ / r,
  { rintros Î¶ (hÎ¶ : abs (Î¶ - z) = r),
    simpa [norm_smul, hÎ¶, â† div_eq_inv_mul] using (div_le_div_right hr).2 (hz (hsub' hÎ¶)) },
  have hlt : âˆ¥(w - z)â»Â¹ â€¢ f wâˆ¥ < âˆ¥f zâˆ¥ / r,
    by simpa [norm_smul, â† div_eq_inv_mul] using (div_lt_div_right hr).2 hw_lt,
  have : âˆ¥âˆ® Î¶ in C(z, r), (Î¶ - z)â»Â¹ â€¢ f Î¶âˆ¥ < 2 * Ï€ * r * (âˆ¥f zâˆ¥ / r),
    from circle_integral.norm_integral_lt_of_norm_le_const_of_lt hr hcont hle âŸ¨w, rfl, hltâŸ©,
  refine this.ne _,
  rw circle_integral_sub_inv_smul_of_differentiable_on (mem_ball_self hr) (hd.mono hsub),
  field_simp [norm_smul, hr.ne', abs_of_pos real.pi_pos],
  ac_refl
end

/-- If `f : E â†’ F` is complex differentiable on a set `s`, the norm of `f` takes it maximum on `s`
at `z` and `w` is a point such that the closed ball with center `z` and radius `dist w z` is
included in `s`, then `âˆ¥f wâˆ¥ = âˆ¥f zâˆ¥`. -/
lemma norm_eq_norm_of_is_max_on_of_closed_ball_subset {f : E â†’ F} {s : set E} {z w : E}
  (hd : differentiable_on â„‚ f s) (hz : is_max_on (norm âˆ˜ f) s z)
  (hsub : closed_ball z (dist w z) âŠ† s) :
  âˆ¥f wâˆ¥ = âˆ¥f zâˆ¥ :=
begin
  set e : â„‚ â†’ E := Î» t : â„‚, t â€¢ (w - z) + z,
  suffices : âˆ¥(f âˆ˜ e) 1âˆ¥ = âˆ¥(f âˆ˜ e) 0âˆ¥, by simpa [e],
  have : differentiable_on â„‚ (f âˆ˜ e) ((Î» t : â„‚, t â€¢ (w - z) + z) â»Â¹' s),
    from hd.comp ((differentiable_on_id.smul_const (w - z)).add_const z) subset.rfl,
  refine norm_eq_norm_of_is_max_on_of_closed_ball_subset_dim1
    this _ _,
  { refine Î» t ht, _, simpa [e] using hz ht },
  { refine Î» t ht, hsub _,
    have : abs t â‰¤ 1, by simpa using ht,
    simpa [dist_eq_norm, norm_smul] using mul_le_of_le_one_left (norm_nonneg (w - z)) this }
end

lemma norm_eventually_eq_of_is_local_max {f : E â†’ F} {c : E}
  (hd : âˆ€á¶  z in ð“ c, differentiable_at â„‚ f z) (hc : is_local_max (norm âˆ˜ f) c) :
  âˆ€á¶  y in ð“ c, âˆ¥f yâˆ¥ = âˆ¥f câˆ¥ :=
begin
  rcases nhds_basis_closed_ball.eventually_iff.1 (hd.and hc) with âŸ¨r, hrâ‚€, hrâŸ©,
  exact nhds_basis_closed_ball.eventually_iff.2 âŸ¨r, hrâ‚€, Î» w hw,
    norm_eq_norm_of_is_max_on_of_closed_ball_subset
      (Î» z hz, (hr hz).1.differentiable_within_at) (Î» z hz, (hr hz).2)
      (closed_ball_subset_closed_ball hw)âŸ©
end

lemma is_open_set_of_mem_nhds_and_is_max_on_norm {f : E â†’ F} {s : set E}
  (hd : differentiable_on â„‚ f s) :
  is_open {z | s âˆˆ ð“ z âˆ§ is_max_on (norm âˆ˜ f) s z} :=
begin
  refine is_open_iff_mem_nhds.2 (Î» z hz, (eventually_eventually_nhds.2 hz.1).and _),
  replace hd : âˆ€á¶  w in ð“ z, differentiable_at â„‚ f w, from hd.eventually_differentiable_at hz.1,
  exact (norm_eventually_eq_of_is_local_max hd $ (hz.2.is_local_max hz.1)).mono
    (Î» x hx y hy, le_trans (hz.2 hy) hx.ge)
end

/-- **Maximum modulus principle**: if `f : E â†’ F` is complex differentiable on a nonempty compact
set `K`, then there exists a point `z âˆˆ frontier K` such that `Î» z, âˆ¥f zâˆ¥` takes it maximum value on
`K` at `z`. -/
lemma exists_mem_frontier_is_max_on_norm [nontrivial E] [finite_dimensional â„‚ E]
  {f : E â†’ F} {K : set E} (hs : is_compact K)
  (hne : K.nonempty) (hd : differentiable_on â„‚ f K) :
  âˆƒ z âˆˆ frontier K, is_max_on (norm âˆ˜ f) K z :=
begin
  rcases hs.exists_forall_ge hne hd.continuous_on.norm with âŸ¨w, hws, hleâŸ©,
  rcases exists_mem_frontier_inf_dist_compl_eq_dist hws hs.ne_univ with âŸ¨z, hzs, hzwâŸ©,
  refine âŸ¨z, hzs, Î» x hx, (hle x hx).trans_eq _âŸ©,
  refine (norm_eq_norm_of_is_max_on_of_closed_ball_subset hd hle _).symm,
  calc closed_ball w (dist z w) âŠ† closed_ball w (inf_dist w Ká¶œ) :
    closed_ball_subset_closed_ball (by rw [hzw, dist_comm])
  ... âŠ† closure K : closed_ball_inf_dist_compl_subset_closure hws hs.ne_univ
  ... = K : hs.is_closed.closure_eq
end

/-- **Maximum modulus principle**: if `f : E â†’ F` is complex differentiable on a compact set `s` and
`âˆ¥f zâˆ¥ â‰¤ C` for any `z âˆˆ frontier s`, then the same is true for any `z âˆˆ s`. -/
lemma norm_le_of_forall_mem_frontier_norm_le [nontrivial E] [finite_dimensional â„‚ E]
  {f : E â†’ F} {s : set E} (hs : is_compact s) (hd : differentiable_on â„‚ f s)
  {C : â„} (hC : âˆ€ z âˆˆ frontier s, âˆ¥f zâˆ¥ â‰¤ C) {z : E} (hz : z âˆˆ s) :
  âˆ¥f zâˆ¥ â‰¤ C :=
let âŸ¨w, hws, hwâŸ© := exists_mem_frontier_is_max_on_norm hs âŸ¨z, hzâŸ© hd in le_trans (hw hz) (hC w hws)

end complex
