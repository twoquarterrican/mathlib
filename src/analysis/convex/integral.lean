/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import analysis.convex.function
import analysis.convex.strict
import measure_theory.function.ae_eq_of_integral

/-!
# Jensen's inequality for integrals

In this file we define `measure_theory.average Î¼ f` (notation: `â¨ x, f x âˆ‚Î¼`) to be the average
value of `f` with respect to measure `Î¼`. It is defined as `(Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼`, so it
is equal to zero if `f` is not integrable or if `Î¼` is an infinite measure. If `Î¼` is a probability
measure, then the average of any function is equal to its integral.

Then we prove several forms of Jensen's inequality for integrals.

- for convex sets: `convex.average_mem`, `convex.set_average_mem`, `convex.integral_mem`;

- for convex functions: `convex.on.average_mem_epigraph`, `convex_on.map_average_le`,
  `convex_on.set_average_mem_epigraph`, `convex_on.map_set_average_le`, `convex_on.map_integral_le`;

- for strictly convex sets: `strict_convex.ae_eq_const_or_average_mem_interior`;

- for a closed ball in a strictly convex normed space:
  `strict_convex.ae_eq_const_or_norm_integral_lt_of_norm_le_const`

- for strictly convex functions: `strict_convex_on.ae_eq_const_or_map_average_lt`.

## TODO

- Use a typeclass for strict convexity of a closed ball.

## Tags

convex, integral, center mass, average value, Jensen's inequality
-/

open measure_theory measure_theory.measure metric set filter topological_space function
open_locale topological_space big_operators ennreal convex

variables {Î± E F : Type*} {m0 : measurable_space Î±}
  [normed_group E] [normed_space â„ E] [complete_space E]
  [topological_space.second_countable_topology E] [measurable_space E] [borel_space E]
  [normed_group F] [normed_space â„ F] [complete_space F]
  [topological_space.second_countable_topology F] [measurable_space F] [borel_space F]
  {Î¼ : measure Î±} {s : set E}

/-!
### Average value of a function w.r.t. a measure

The average value of a function `f` w.r.t. a measure `Î¼` (notation: `â¨ x, f x âˆ‚Î¼`) is defined as
`(Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼`, so it is equal to zero if `f` is not integrable or if `Î¼` is an
infinite measure. If `Î¼` is a probability measure, then the average of any function is equal to its
integral.

For the average on a set, use `â¨ x in s, f x âˆ‚Î¼` (defined as `â¨ x, f x âˆ‚(Î¼.restrict s)`). For
average w.r.t. the volume, one can omit `âˆ‚volume`.
-/

namespace measure_theory

variable (Î¼)
include m0

/-- Average value of a function `f` w.r.t. a measure `Î¼`, notation: `â¨ x, f x âˆ‚Î¼`. It is defined as
`(Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼`, so it is equal to zero if `f` is not integrable or if `Î¼` is an
infinite measure. If `Î¼` is a probability measure, then the average of any function is equal to its
integral.

For the average on a set, use `â¨ x in s, f x âˆ‚Î¼` (defined as `â¨ x, f x âˆ‚(Î¼.restrict s)`). For
average w.r.t. the volume, one can omit `âˆ‚volume`. -/
noncomputable def average (f : Î± â†’ E) := âˆ« x, f x âˆ‚((Î¼ univ)â»Â¹ â€¢ Î¼)

notation `â¨` binders `, ` r:(scoped:60 f, f) ` âˆ‚` Î¼:70 := average Î¼ r
notation `â¨` binders `, ` r:(scoped:60 f, average volume f) := r
notation `â¨` binders ` in ` s `, ` r:(scoped:60 f, f) ` âˆ‚` Î¼:70 := average (measure.restrict Î¼ s) r
notation `â¨` binders ` in ` s `, ` r:(scoped:60 f, average (measure.restrict volume s) f) := r

@[simp] lemma average_zero : â¨ x, (0 : E) âˆ‚Î¼ = 0 := by rw [average, integral_zero]

@[simp] lemma average_zero_measure (f : Î± â†’ E) : â¨ x, f x âˆ‚(0 : measure Î±) = 0 :=
by rw [average, smul_zero, integral_zero_measure]

@[simp] lemma average_neg (f : Î± â†’ E) : â¨ x, -f x âˆ‚Î¼ = -â¨ x, f x âˆ‚Î¼ := integral_neg f

lemma average_def (f : Î± â†’ E) : â¨ x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚((Î¼ univ)â»Â¹ â€¢ Î¼) := rfl

lemma average_def' (f : Î± â†’ E) : â¨ x, f x âˆ‚Î¼ = (Î¼ univ).to_realâ»Â¹ â€¢ âˆ« x, f x âˆ‚Î¼ :=
by rw [average_def, integral_smul_measure, ennreal.to_real_inv]

lemma average_eq_integral [is_probability_measure Î¼] (f : Î± â†’ E) :
  â¨ x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
by rw [average, measure_univ, ennreal.inv_one, one_smul]

@[simp] lemma measure_smul_average [is_finite_measure Î¼] (f : Î± â†’ E) :
  (Î¼ univ).to_real â€¢ â¨ x, f x âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
begin
  cases eq_or_ne Î¼ 0 with hÎ¼ hÎ¼,
  { rw [hÎ¼, integral_zero_measure, average_zero_measure, smul_zero] },
  { rw [average_def', smul_inv_smulâ‚€],
    refine (ennreal.to_real_pos _ $ measure_ne_top _ _).ne',
    rwa [ne.def, measure_univ_eq_zero] }
end

lemma set_average_eq (f : Î± â†’ E) (s : set Î±) :
  â¨ x in s, f x âˆ‚Î¼ = (Î¼ s).to_realâ»Â¹ â€¢ âˆ« x in s, f x âˆ‚Î¼ :=
by rw [average_def', restrict_apply_univ]

variable {Î¼}

lemma average_congr {f g : Î± â†’ E} (h : f =áµ[Î¼] g) : â¨ x, f x âˆ‚Î¼ = â¨ x, g x âˆ‚Î¼ :=
by simp only [average_def', integral_congr_ae h]

lemma average_add_measure [is_finite_measure Î¼] {Î½ : measure Î±} [is_finite_measure Î½] {f : Î± â†’ E}
  (hÎ¼ : integrable f Î¼) (hÎ½ : integrable f Î½) :
  â¨ x, f x âˆ‚(Î¼ + Î½) =
    ((Î¼ univ).to_real / ((Î¼ univ).to_real + (Î½ univ).to_real)) â€¢ â¨ x, f x âˆ‚Î¼ +
      ((Î½ univ).to_real / ((Î¼ univ).to_real + (Î½ univ).to_real)) â€¢ â¨ x, f x âˆ‚Î½ :=
begin
  simp only [div_eq_inv_mul, mul_smul, measure_smul_average, â† smul_add,
    â† integral_add_measure hÎ¼ hÎ½, â† ennreal.to_real_add (measure_ne_top Î¼ _) (measure_ne_top Î½ _)],
  rw [average_def', measure.add_apply]
end

lemma average_pair {f : Î± â†’ E} {g : Î± â†’ F} (hfi : integrable f Î¼) (hgi : integrable g Î¼) :
  â¨ x, (f x, g x) âˆ‚Î¼ = (â¨ x, f x âˆ‚Î¼, â¨ x, g x âˆ‚Î¼) :=
integral_pair hfi.to_average hgi.to_average

lemma measure_smul_set_average (f : Î± â†’ E) {s : set Î±} (h : Î¼ s â‰  âˆ) :
  (Î¼ s).to_real â€¢ â¨ x in s, f x âˆ‚Î¼ = âˆ« x in s, f x âˆ‚Î¼ :=
by { haveI := fact.mk h.lt_top, rw [â† measure_smul_average, restrict_apply_univ] }

lemma average_union {f : Î± â†’ E} {s t : set Î±} (hd : ae_disjoint Î¼ s t)
  (ht : null_measurable_set t Î¼) (hsÎ¼ : Î¼ s â‰  âŠ¤) (htÎ¼ : Î¼ t â‰  âŠ¤)
  (hfs : integrable_on f s Î¼) (hft : integrable_on f t Î¼) :
  â¨ x in s âˆª t, f x âˆ‚Î¼ =
    ((Î¼ s).to_real / ((Î¼ s).to_real + (Î¼ t).to_real)) â€¢ â¨ x in s, f x âˆ‚Î¼ +
      ((Î¼ t).to_real / ((Î¼ s).to_real + (Î¼ t).to_real)) â€¢ â¨ x in t, f x âˆ‚Î¼ :=
begin
  haveI := fact.mk hsÎ¼.lt_top, haveI := fact.mk htÎ¼.lt_top,
  rw [restrict_unionâ‚€ hd ht, average_add_measure hfs hft, restrict_apply_univ, restrict_apply_univ]
end

lemma average_union_mem_open_segment {f : Î± â†’ E} {s t : set Î±} (hd : ae_disjoint Î¼ s t)
  (ht : null_measurable_set t Î¼) (hsâ‚€ : Î¼ s â‰  0) (htâ‚€ : Î¼ t â‰  0) (hsÎ¼ : Î¼ s â‰  âŠ¤) (htÎ¼ : Î¼ t â‰  âŠ¤)
  (hfs : integrable_on f s Î¼) (hft : integrable_on f t Î¼) :
  â¨ x in s âˆª t, f x âˆ‚Î¼ âˆˆ open_segment â„ (â¨ x in s, f x âˆ‚Î¼) (â¨ x in t, f x âˆ‚Î¼) :=
begin
  replace hsâ‚€ : 0 < (Î¼ s).to_real, from ennreal.to_real_pos hsâ‚€ hsÎ¼,
  replace htâ‚€ : 0 < (Î¼ t).to_real, from ennreal.to_real_pos htâ‚€ htÎ¼,
  refine mem_open_segment_iff_div.mpr âŸ¨(Î¼ s).to_real, (Î¼ t).to_real, hsâ‚€, htâ‚€,
    (average_union hd ht hsÎ¼ htÎ¼ hfs hft).symmâŸ©
end

lemma average_union_mem_segment {f : Î± â†’ E} {s t : set Î±} (hd : ae_disjoint Î¼ s t)
  (ht : null_measurable_set t Î¼) (hsÎ¼ : Î¼ s â‰  âŠ¤) (htÎ¼ : Î¼ t â‰  âŠ¤)
  (hfs : integrable_on f s Î¼) (hft : integrable_on f t Î¼) :
  â¨ x in s âˆª t, f x âˆ‚Î¼ âˆˆ [â¨ x in s, f x âˆ‚Î¼ -[â„] â¨ x in t, f x âˆ‚Î¼] :=
begin
  by_cases hse : Î¼ s = 0,
  { rw â† ae_eq_empty at hse,
    rw [restrict_congr_set (hse.union eventually_eq.rfl), empty_union],
    exact right_mem_segment _ _ _ },
  { refine mem_segment_iff_div.mpr âŸ¨(Î¼ s).to_real, (Î¼ t).to_real, ennreal.to_real_nonneg,
      ennreal.to_real_nonneg, _, (average_union hd ht hsÎ¼ htÎ¼ hfs hft).symmâŸ©,
    calc 0 < (Î¼ s).to_real : ennreal.to_real_pos hse hsÎ¼
    ... â‰¤ _ : le_add_of_nonneg_right ennreal.to_real_nonneg }
end

lemma average_mem_open_segment_compl_self [is_finite_measure Î¼] {f : Î± â†’ E} {s : set Î±}
  (hs : null_measurable_set s Î¼) (hsâ‚€ : Î¼ s â‰  0) (hscâ‚€ : Î¼ sá¶œ â‰  0) (hfi : integrable f Î¼) :
  â¨ x, f x âˆ‚Î¼ âˆˆ open_segment â„ (â¨ x in s, f x âˆ‚Î¼) (â¨ x in sá¶œ, f x âˆ‚Î¼) :=
by simpa only [union_compl_self, restrict_univ]
  using average_union_mem_open_segment ae_disjoint_compl_right hs.compl hsâ‚€ hscâ‚€
    (measure_ne_top _ _) (measure_ne_top _ _) hfi.integrable_on hfi.integrable_on

end measure_theory

open measure_theory

/-!
### Non-strict Jensen's inequality
-/

/-- If `Î¼` is a probability measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the expected value of `f` belongs to `s`:
`âˆ« x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.sum_mem` for a finite sum version of this lemma. -/
lemma convex.integral_mem [is_probability_measure Î¼] {s : set E} (hs : convex â„ s)
  (hsc : is_closed s) {f : Î± â†’ E} (hf : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) :
  âˆ« x, f x âˆ‚Î¼ âˆˆ s :=
begin
  obtain âŸ¨yâ‚€, hâ‚€âŸ© : s.nonempty,
  { rcases hf.exists with âŸ¨xâ‚€, hâ‚€âŸ©, exact âŸ¨f xâ‚€, hâ‚€âŸ© },
  rcases hfi.ae_measurable with âŸ¨g, hgm, hfgâŸ©,
  rw [integral_congr_ae hfg], rw [integrable_congr hfg] at hfi,
  have hg : âˆ€áµ x âˆ‚Î¼, g x âˆˆ closure s,
    from (hfg.rw (Î» x y, y âˆˆ s) hf).mono (Î» x hx, subset_closure hx),
  set G : â„• â†’ simple_func Î± E := simple_func.approx_on _ hgm s yâ‚€ hâ‚€,
  have : tendsto (Î» n, (G n).integral Î¼) at_top (ğ“ $ âˆ« x, g x âˆ‚Î¼),
    from tendsto_integral_approx_on_of_measurable hfi _ hg _ (integrable_const _),
  refine hsc.mem_of_tendsto this (eventually_of_forall $ Î» n, hs.sum_mem _ _ _),
  { exact Î» _ _, ennreal.to_real_nonneg },
  { rw [â† ennreal.to_real_sum, (G n).sum_range_measure_preimage_singleton, measure_univ,
      ennreal.one_to_real],
    exact Î» _ _, measure_ne_top _ _ },
  { simp only [simple_func.mem_range, forall_range_iff],
    exact Î» x, simple_func.approx_on_mem hgm _ _ _ },
end

/-- If `Î¼` is a non-zero finite measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the average value of `f` belongs to `s`:
`â¨ x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.center_mass_mem` for a finite sum version of this lemma. -/
lemma convex.average_mem [is_finite_measure Î¼] {s : set E} (hs : convex â„ s) (hsc : is_closed s)
  (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) :
  â¨ x, f x âˆ‚Î¼ âˆˆ s :=
begin
  haveI : is_probability_measure ((Î¼ univ)â»Â¹ â€¢ Î¼),
    from is_probability_measure_smul hÎ¼,
  refine hs.integral_mem hsc (ae_mono' _ hfs) hfi.to_average,
  exact absolutely_continuous.smul (refl _) _
end

/-- If `Î¼` is a non-zero finite measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the average value of `f` belongs to `s`:
`â¨ x, f x âˆ‚Î¼ âˆˆ s`. See also `convex.center_mass_mem` for a finite sum version of this lemma. -/
lemma convex.set_average_mem {t : set Î±} {s : set E} (hs : convex â„ s) (hsc : is_closed s)
  (h0 : Î¼ t â‰  0) (ht : Î¼ t â‰  âˆ) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s)
  (hfi : integrable_on f t Î¼) :
  â¨ x in t, f x âˆ‚Î¼ âˆˆ s :=
begin
  haveI : fact (Î¼ t < âˆ) := âŸ¨ht.lt_topâŸ©,
  refine hs.average_mem hsc _ hfs hfi,
  rwa [ne.def, restrict_eq_zero]
end

lemma convex_on.average_mem_epigraph [is_finite_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : convex_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  (â¨ x, f x âˆ‚Î¼, â¨ x, g (f x) âˆ‚Î¼) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ g p.1 â‰¤ p.2} :=
have ht_mem : âˆ€áµ x âˆ‚Î¼, (f x, g (f x)) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ g p.1 â‰¤ p.2},
  from hfs.mono (Î» x hx, âŸ¨hx, le_rflâŸ©),
by simpa only [average_pair hfi hgi]
  using hg.convex_epigraph.average_mem (hsc.epigraph hgc) hÎ¼ ht_mem (hfi.prod_mk hgi)

lemma concave_on.average_mem_hypograph [is_finite_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : concave_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  (â¨ x, f x âˆ‚Î¼, â¨ x, g (f x) âˆ‚Î¼) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ p.2 â‰¤ g p.1} :=
by simpa only [mem_set_of_eq, pi.neg_apply, average_neg, neg_le_neg_iff]
  using hg.neg.average_mem_epigraph hgc.neg hsc hÎ¼ hfs hfi hgi.neg

/-- Jensen's inequality: if a function `g : E â†’ â„` is convex and continuous on a convex closed set
`s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points
to `s`, then the value of `g` at the average value of `f` is less than or equal to the average value
of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are integrable. See also
`convex_on.map_center_mass_le` for a finite sum version of this lemma. -/
lemma convex_on.map_average_le [is_finite_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : convex_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  g (â¨ x, f x âˆ‚Î¼) â‰¤ â¨ x, g (f x) âˆ‚Î¼ :=
(hg.average_mem_epigraph hgc hsc hÎ¼ hfs hfi hgi).2

/-- Jensen's inequality: if a function `g : E â†’ â„` is concave and continuous on a convex closed set
`s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points
to `s`, then the average value of `g âˆ˜ f` is less than or equal to the value of `g` at the average
value of `f` provided that both `f` and `g âˆ˜ f` are integrable. See also
`concave_on.le_map_center_mass` for a finite sum version of this lemma. -/
lemma concave_on.le_map_average [is_finite_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : concave_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) (hÎ¼ : Î¼ â‰  0) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  â¨ x, g (f x) âˆ‚Î¼ â‰¤ g (â¨ x, f x âˆ‚Î¼) :=
(hg.average_mem_hypograph hgc hsc hÎ¼ hfs hfi hgi).2

/-- Jensen's inequality: if a function `g : E â†’ â„` is convex and continuous on a convex closed set
`s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points
of a set `t` to `s`, then the value of `g` at the average value of `f` over `t` is less than or
equal to the average value of `g âˆ˜ f` over `t` provided that both `f` and `g âˆ˜ f` are
integrable. -/
lemma convex_on.set_average_mem_epigraph {s : set E} {g : E â†’ â„} (hg : convex_on â„ s g)
  (hgc : continuous_on g s) (hsc : is_closed s) {t : set Î±} (h0 : Î¼ t â‰  0)
  (ht : Î¼ t â‰  âˆ) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s) (hfi : integrable_on f t Î¼)
  (hgi : integrable_on (g âˆ˜ f) t Î¼) :
  (â¨ x in t, f x âˆ‚Î¼, â¨ x in t, g (f x) âˆ‚Î¼) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ g p.1 â‰¤ p.2} :=
begin
  haveI : fact (Î¼ t < âˆ) := âŸ¨ht.lt_topâŸ©,
  refine hg.average_mem_epigraph hgc hsc _ hfs hfi hgi,
  rwa [ne.def, restrict_eq_zero]
end

/-- Jensen's inequality: if a function `g : E â†’ â„` is concave and continuous on a convex closed set
`s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points
of a set `t` to `s`, then the average value of `g âˆ˜ f` over `t` is less than or equal to the value
of `g` at the average value of `f` over `t` provided that both `f` and `g âˆ˜ f` are integrable. -/
lemma concave_on.set_average_mem_hypograph {s : set E} {g : E â†’ â„} (hg : concave_on â„ s g)
  (hgc : continuous_on g s) (hsc : is_closed s) {t : set Î±} (h0 : Î¼ t â‰  0)
  (ht : Î¼ t â‰  âˆ) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s) (hfi : integrable_on f t Î¼)
  (hgi : integrable_on (g âˆ˜ f) t Î¼) :
  (â¨ x in t, f x âˆ‚Î¼, â¨ x in t, g (f x) âˆ‚Î¼) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ p.2 â‰¤ g p.1} :=
by simpa only [mem_set_of_eq, pi.neg_apply, average_neg, neg_le_neg_iff]
  using hg.neg.set_average_mem_epigraph hgc.neg hsc h0 ht hfs hfi hgi.neg

/-- Jensen's inequality: if a function `g : E â†’ â„` is convex and continuous on a convex closed set
`s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points
of a set `t` to `s`, then the value of `g` at the average value of `f` over `t` is less than or
equal to the average value of `g âˆ˜ f` over `t` provided that both `f` and `g âˆ˜ f` are
integrable. -/
lemma convex_on.map_set_average_le {s : set E} {g : E â†’ â„} (hg : convex_on â„ s g)
  (hgc : continuous_on g s) (hsc : is_closed s) {t : set Î±} (h0 : Î¼ t â‰  0)
  (ht : Î¼ t â‰  âˆ) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s) (hfi : integrable_on f t Î¼)
  (hgi : integrable_on (g âˆ˜ f) t Î¼) :
  g (â¨ x in t, f x âˆ‚Î¼) â‰¤ â¨ x in t, g (f x) âˆ‚Î¼ :=
(hg.set_average_mem_epigraph hgc hsc h0 ht hfs hfi hgi).2

/-- Jensen's inequality: if a function `g : E â†’ â„` is concave and continuous on a convex closed set
`s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e. points
of a set `t` to `s`, then the average value of `g âˆ˜ f` over `t` is less than or equal to the value
of `g` at the average value of `f` over `t` provided that both `f` and `g âˆ˜ f` are integrable. -/
lemma concave_on.le_map_set_average {s : set E} {g : E â†’ â„} (hg : concave_on â„ s g)
  (hgc : continuous_on g s) (hsc : is_closed s) {t : set Î±} (h0 : Î¼ t â‰  0)
  (ht : Î¼ t â‰  âˆ) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s) (hfi : integrable_on f t Î¼)
  (hgi : integrable_on (g âˆ˜ f) t Î¼) :
  â¨ x in t, g (f x) âˆ‚Î¼ â‰¤ g (â¨ x in t, f x âˆ‚Î¼) :=
(hg.set_average_mem_hypograph hgc hsc h0 ht hfs hfi hgi).2

/-- Convex **Jensen's inequality**: if a function `g : E â†’ â„` is convex and continuous on a convex
closed set `s`, `Î¼` is a probability measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e.
points to `s`, then the value of `g` at the expected value of `f` is less than or equal to the
expected value of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are integrable. See also
`convex_on.map_center_mass_le` for a finite sum version of this lemma. -/
lemma convex_on.map_integral_le [is_probability_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : convex_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  g (âˆ« x, f x âˆ‚Î¼) â‰¤ âˆ« x, g (f x) âˆ‚Î¼ :=
by simpa only [average_eq_integral]
  using hg.map_average_le hgc hsc (is_probability_measure.ne_zero Î¼) hfs hfi hgi

/-- Convex **Jensen's inequality**: if a function `g : E â†’ â„` is concave and continuous on a convex
closed set `s`, `Î¼` is a probability measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e.
points to `s`, then the expected value of `g âˆ˜ f` is less than or equal to the value of `g` at the
expected value of `f` provided that both `f` and `g âˆ˜ f` are integrable. -/
lemma concave_on.le_map_integral [is_probability_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : concave_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  âˆ« x, g (f x) âˆ‚Î¼ â‰¤ g (âˆ« x, f x âˆ‚Î¼) :=
by simpa only [average_eq_integral]
  using hg.le_map_average hgc hsc (is_probability_measure.ne_zero Î¼) hfs hfi hgi

/-- If `f : Î± â†’ E` is an integrable function, then either it is a.e. equal to the constant
`â¨ x, f x âˆ‚Î¼` or there exists a measurable set such that `Î¼ s â‰  0`, `Î¼ sá¶œ â‰  0`, and the average
values of `f` over `s` and `sá¶œ` are different. -/
lemma measure_theory.integrable.ae_eq_const_or_exists_average_ne_compl [is_finite_measure Î¼]
  {f : Î± â†’ E} (hfi : integrable f Î¼) :
  (f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼)) âˆ¨ âˆƒ s, measurable_set s âˆ§ Î¼ s â‰  0 âˆ§ Î¼ sá¶œ â‰  0 âˆ§
    â¨ x in s, f x âˆ‚Î¼ â‰  â¨ x in sá¶œ, f x âˆ‚Î¼ :=
begin
  refine or_iff_not_imp_right.mpr (Î» H, _), push_neg at H,
  refine hfi.ae_eq_of_forall_set_integral_eq _ _ (integrable_const _) (Î» s hs hs', _), clear hs',
  simp only [const_apply, set_integral_const],
  by_cases hâ‚€ : Î¼ s = 0,
  { rw [restrict_eq_zero.2 hâ‚€, integral_zero_measure, hâ‚€, ennreal.zero_to_real, zero_smul] },
  by_cases hâ‚€' : Î¼ sá¶œ = 0,
  { rw â† ae_eq_univ at hâ‚€',
    rw [restrict_congr_set hâ‚€', restrict_univ, measure_congr hâ‚€', measure_smul_average] },
  have := average_mem_open_segment_compl_self hs.null_measurable_set hâ‚€ hâ‚€' hfi,
  rw [â† H s hs hâ‚€ hâ‚€', open_segment_same, mem_singleton_iff] at this,
  rw [this, measure_smul_set_average _ (measure_ne_top Î¼ _)]
end

/-- **Jensen's inequality**, strict version: if an integrable function `f : Î± â†’ E` takes values in a
convex closed set `s` and for some set `t` of positive measure, the average value of `f` over `t`
belongs to the interior of `s`, then the average of `f` over the whole space belongs to the interior
of `s`. -/
lemma convex.average_mem_interior_of_set [is_finite_measure Î¼] {t : set Î±} {s : set E}
  (hs : convex â„ s) (hsc : is_closed s) (h0 : Î¼ t â‰  0) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s)
  (hfi : integrable f Î¼) (ht : â¨ x in t, f x âˆ‚Î¼ âˆˆ interior s) :
  â¨ x, f x âˆ‚Î¼ âˆˆ interior s :=
begin
  rw â† measure_to_measurable at h0, rw â† restrict_to_measurable (measure_ne_top Î¼ t) at ht,
  by_cases h0' : Î¼ (to_measurable Î¼ t)á¶œ = 0,
  { rw â† ae_eq_univ at h0',
    rwa [restrict_congr_set h0', restrict_univ] at ht },
  exact hs.open_segment_subset_interior_left ht
    (hs.set_average_mem hsc h0' (measure_ne_top _ _) (ae_restrict_of_ae hfs) hfi.integrable_on)
    (average_mem_open_segment_compl_self (measurable_set_to_measurable Î¼ t).null_measurable_set
      h0 h0' hfi)
end

/-- **Jensen's inequality**, strict version: if an integrable function `f : Î± â†’ E` takes values in a
strictly convex closed set `s`, then either it is a.e. equal to its average value, or its average
value belongs to the interior of `s`. -/
lemma strict_convex.ae_eq_const_or_average_mem_interior [is_finite_measure Î¼] {s : set E}
  (hs : strict_convex â„ s) (hsc : is_closed s) {f : Î± â†’ E} (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s)
  (hfi : integrable f Î¼) :
  f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼) âˆ¨ â¨ x, f x âˆ‚Î¼ âˆˆ interior s :=
begin
  have : âˆ€ {t}, Î¼ t â‰  0 â†’ â¨ x in t, f x âˆ‚Î¼ âˆˆ s,
    from Î» t ht, hs.convex.set_average_mem hsc ht (measure_ne_top _ _) (ae_restrict_of_ae hfs)
      hfi.integrable_on,
  refine hfi.ae_eq_const_or_exists_average_ne_compl.imp_right _,
  rintro âŸ¨t, hm, hâ‚€, hâ‚€', hneâŸ©,
  exact hs.open_segment_subset (this hâ‚€) (this hâ‚€') hne
    (average_mem_open_segment_compl_self hm.null_measurable_set hâ‚€ hâ‚€' hfi)
end

/-- **Jensen's inequality**, strict version: if an integrable function `f : Î± â†’ E` takes values in a
convex closed set `s`, and `g : E â†’ â„` is continuous and strictly convex on `s`, then
either `f` is a.e. equal to its average value, or `g (â¨ x, f x âˆ‚Î¼) < â¨ x, g (f x) âˆ‚Î¼`. -/
lemma strict_convex_on.ae_eq_const_or_map_average_lt [is_finite_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : strict_convex_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼) âˆ¨ g (â¨ x, f x âˆ‚Î¼) < â¨ x, g (f x) âˆ‚Î¼ :=
begin
  have : âˆ€ {t}, Î¼ t â‰  0 â†’ â¨ x in t, f x âˆ‚Î¼ âˆˆ s âˆ§ g (â¨ x in t, f x âˆ‚Î¼) â‰¤ â¨ x in t, g (f x) âˆ‚Î¼,
    from Î» t ht, hg.convex_on.set_average_mem_epigraph hgc hsc ht (measure_ne_top _ _)
      (ae_restrict_of_ae hfs) hfi.integrable_on hgi.integrable_on,
  refine hfi.ae_eq_const_or_exists_average_ne_compl.imp_right _,
  rintro âŸ¨t, hm, hâ‚€, hâ‚€', hneâŸ©,
  rcases average_mem_open_segment_compl_self hm.null_measurable_set hâ‚€ hâ‚€' (hfi.prod_mk hgi)
    with âŸ¨a, b, ha, hb, hab, h_avgâŸ©,
  simp only [average_pair hfi hgi, average_pair hfi.integrable_on hgi.integrable_on, prod.smul_mk,
    prod.mk_add_mk, prod.mk.inj_iff, (âˆ˜)] at h_avg,
  rw [â† h_avg.1, â† h_avg.2],
  calc g (a â€¢ â¨ x in t, f x âˆ‚Î¼ + b â€¢ â¨ x in tá¶œ, f x âˆ‚Î¼)
      < a * g (â¨ x in t, f x âˆ‚Î¼) + b * g (â¨ x in tá¶œ, f x âˆ‚Î¼) :
    hg.2 (this hâ‚€).1 (this hâ‚€').1 hne ha hb hab
  ... â‰¤ a * â¨ x in t, g (f x) âˆ‚Î¼ + b * â¨ x in tá¶œ, g (f x) âˆ‚Î¼ :
    add_le_add (mul_le_mul_of_nonneg_left (this hâ‚€).2 ha.le)
      (mul_le_mul_of_nonneg_left (this hâ‚€').2 hb.le)
end

/-- **Jensen's inequality**, strict version: if an integrable function `f : Î± â†’ E` takes values in a
convex closed set `s`, and `g : E â†’ â„` is continuous and strictly concave on `s`, then
either `f` is a.e. equal to its average value, or `â¨ x, g (f x) âˆ‚Î¼ < g (â¨ x, f x âˆ‚Î¼)`. -/
lemma strict_concave_on.ae_eq_const_or_lt_map_average [is_finite_measure Î¼] {s : set E} {g : E â†’ â„}
  (hg : strict_concave_on â„ s g) (hgc : continuous_on g s) (hsc : is_closed s) {f : Î± â†’ E}
  (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : integrable f Î¼) (hgi : integrable (g âˆ˜ f) Î¼) :
  f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼) âˆ¨ â¨ x, g (f x) âˆ‚Î¼ < g (â¨ x, f x âˆ‚Î¼) :=
by simpa only [pi.neg_apply, average_neg, neg_lt_neg_iff]
  using hg.neg.ae_eq_const_or_map_average_lt hgc.neg hsc hfs hfi hgi.neg

/-- If the closed ball of radius `C` in a normed space `E` is strictly convex and `f : Î± â†’ E` is
a function such that `âˆ¥f xâˆ¥ â‰¤ C` a.e., then either either this function is a.e. equal to its
average value, or the norm of its integral is strictly less than `(Î¼ univ).to_real * C`. -/
lemma strict_convex.ae_eq_const_or_norm_integral_lt_of_norm_le_const [is_finite_measure Î¼]
  {f : Î± â†’ E} {C : â„} (h_convex : strict_convex â„ (closed_ball (0 : E) C))
  (h_le : âˆ€áµ x âˆ‚Î¼, âˆ¥f xâˆ¥ â‰¤ C) :
  (f =áµ[Î¼] const Î± â¨ x, f x âˆ‚Î¼) âˆ¨ âˆ¥âˆ« x, f x âˆ‚Î¼âˆ¥ < (Î¼ univ).to_real * C :=
begin
  cases le_or_lt C 0 with hC0 hC0,
  { have : f =áµ[Î¼] 0, from h_le.mono (Î» x hx, norm_le_zero_iff.1 (hx.trans hC0)),
    simp only [average_congr this, pi.zero_apply, average_zero],
    exact or.inl this },
  cases eq_or_ne Î¼ 0 with hÎ¼ hÎ¼,
  { rw hÎ¼, exact or.inl rfl },
  by_cases hfi : integrable f Î¼, swap,
  { right,
    simpa [integral_undef hfi, hC0, measure_lt_top, ennreal.to_real_pos_iff, pos_iff_ne_zero]
      using hÎ¼ },
  replace h_le : âˆ€áµ x âˆ‚Î¼, f x âˆˆ closed_ball (0 : E) C, by simpa only [mem_closed_ball_zero_iff],
  have hÎ¼' : 0 < (Î¼ univ).to_real,
    from ennreal.to_real_pos (mt measure_univ_eq_zero.1 hÎ¼) (measure_ne_top _ _),
  simpa only [interior_closed_ball _ hC0, mem_ball_zero_iff, average_def', norm_smul,
    real.norm_eq_abs, abs_inv, abs_of_pos hÎ¼', â† div_eq_inv_mul, div_lt_iff' hÎ¼']
    using h_convex.ae_eq_const_or_average_mem_interior is_closed_ball h_le hfi,
end
